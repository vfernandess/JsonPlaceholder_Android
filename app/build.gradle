apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.voidx.jsonplaceholder"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "SERVER_URL", "\"https://jsonplaceholder.typicode.com/\""
        }
        debug {
            buildConfigField "String", "SERVER_URL", "\"https://jsonplaceholder.typicode.com/\""
        }
        mock.initWith(buildTypes.debug)
        mock {
            buildConfigField "String", "SERVER_URL", "\"http://localhost:8080/\""
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testBuildType "mock"
    testOptions {
        animationsDisabled = true
    }

    buildFeatures{
        dataBinding = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation commonDependencies.koin
    implementation commonDependencies.koinScope
    implementation commonDependencies.koinViewModel

    implementation viewDependencies.legacy
    implementation viewDependencies.appCompat
    implementation viewDependencies.androidx
    implementation viewDependencies.constraintLayout
    implementation viewDependencies.fragment
    implementation viewDependencies.navigation
    implementation viewDependencies.navigationFragment
    implementation viewDependencies.recycler
    implementation viewDependencies.lifecycle
    implementation viewDependencies.shimmer
    implementation viewDependencies.glide
    kapt viewDependencies.glideCompiler

    implementation dataDependencies.gson
    implementation dataDependencies.retrofit
    implementation dataDependencies.retrofitLogging
    implementation dataDependencies.gsonConverter
    implementation dataDependencies.retrofitRxJava
    implementation dataDependencies.rxJava
    implementation dataDependencies.rxAndroid
    implementation dataDependencies.room
    implementation dataDependencies.roomRxJava
    kapt dataDependencies.roomCompiler

    testImplementation testDependencies.jUnit
    testImplementation testDependencies.mockk
    testImplementation testDependencies.arch

    mockImplementation testDependencies.fragmentTesting
    androidTestImplementation testDependencies.jUnit
    androidTestImplementation testDependencies.androidxTestRules
    androidTestImplementation testDependencies.androidxTestRunner
    androidTestImplementation testDependencies.espresso
    androidTestImplementation testDependencies.espressoContrib
    androidTestImplementation testDependencies.androidxNavigationTest
    androidTestImplementation testDependencies.mockWebServer
    androidTestImplementation testDependencies.roomTesting
    androidTestImplementation testDependencies.rxIdler
}